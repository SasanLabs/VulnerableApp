//Community Plugins
plugins {
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
    id 'java'
    id 'eclipse'
    id 'idea'
    id 'com.diffplug.spotless' version '6.25.0'
    id 'com.google.cloud.tools.jib' version '3.3.1'
    id 'org.sonarqube' version '3.3'
    id 'jacoco'
}

tasks.named("bootJar") {
    archiveBaseName.set("VulnerableApp")
    archiveVersion.set("1.0.0")
}


configurations {
    all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

sonarqube {
  properties {
    property "sonar.projectKey", "SasanLabs_VulnerableApp"
    property "sonar.organization", "sasanlabs"
    property "sonar.host.url", "https://sonarcloud.io"
    property 'sonar.java.source', '17'
    property 'sonar.java.target', '17'
  }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}


spotless {
    java {
        // Don't enforce the license, just the format.
        clearSteps()
        googleJavaFormat('1.17.0').aosp()
    }
    format 'javascript', {
    	target 'src/main/resources/**/*.js'
    	prettier().config(['filepath': 'file.js'])
  	}
}

jib {
    to {
        image = 'sasanlabs/owasp-vulnerableapp:unreleased'
    }
    from {
        image = 'eclipse-temurin:17-jre-alpine'
    }
    // Set up multi-platform build only if the task is not :jibDockerBuild
    if (!project.gradle.startParameter.taskNames.contains("jibDockerBuild")) {
        logger.info("JIB: Enabling Multi-Platform Images")

        from {
            image = 'eclipse-temurin:17-jre-alpine'
            platforms {
                platform {
                    architecture = 'amd64'
                    os = 'linux'
                }
                platform {
                    architecture = 'arm64'
                    os = 'linux'
                }
            }
        }
    }
}

jacoco {
    toolVersion = "0.8.11"
}

tasks.register('GenerateSampleVulnerability'){
	group = 'SasanLabs'
	description = 'Generates Sample Vulnerability template'
	println 'Copying SampleVulnerability java file to org.sasanlabs.service.vulnerability.sampleVulnerability package'
	copy {
    	from(file('src/main/resources/sampleVulnerability/sampleVulnerability'))
    	into(file('src/main/java/org/sasanlabs/service/vulnerability/sampleVulnerability'))
    }
    println 'Copy of java file is completed'
    println 'Copying SampleVulnerability html/css/js files to static/templates/SampleVulnerability/LEVEL_1'
    copy {
    	from(file('src/main/resources/sampleVulnerability/staticResources/LEVEL_1'))
    	into(file('src/main/resources/static/templates/SampleVulnerability/LEVEL_1'))
    }
    println 'Copy of html/css/js files is completed'
    println 'SampleVulnerability is generated !!!'
    enabled = false
}

dependencies {
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web'

    // https://mvnrepository.com/artifact/org.mockito/mockito-core
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.5.13'

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.7.0'

    // https://mvnrepository.com/artifact/org.assertj/assertj-core
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.17.2'

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-test
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // https://mvnrepository.com/artifact/org.springframework.boot/spring-boot-starter-data-jpa
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa'

    implementation 'javax.xml.bind:jaxb-api:2.3.1'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.1'

    runtimeOnly group:'com.h2database', name:'h2'
    
    // https://mvnrepository.com/artifact/org.apache.commons/commons-lang3
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.8'
	//Log4j Dependencies
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-log4j2'

    // https://mvnrepository.com/artifact/org.json/json
    implementation group: 'org.json', name: 'json', version: '20190722'

	//https://mvnrepository.com/artifact/com.nimbusds/nimbus-jose-jwt
    implementation group: 'com.nimbusds', name: 'nimbus-jose-jwt', version: '9.31'

    // https://mvnrepository.com/artifact/commons-io/commons-io
    implementation group: 'commons-io', name: 'commons-io', version: '2.7'
    
    implementation group: 'io.github.sasanlabs', name: 'facade-schema', version: '1.0.1'

    implementation group: 'commons-fileupload', name: 'commons-fileupload', version: '1.5'
    
    // https://mvnrepository.com/artifact/commons-codec/commons-codec
    implementation group: 'commons-codec', name: 'commons-codec', version: '1.15'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

tasks.named("jacocoTestReport") {
    dependsOn(tasks.test)

    reports {
        xml.required.set(true)
        html.required.set(true)
        csv.required.set(false)

        xml.outputLocation.set(layout.buildDirectory.file("reports/jacoco.xml"))
    }
}
